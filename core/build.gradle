import java.text.SimpleDateFormat
import java.util.Date
import org.apache.tools.ant.filters.*

apply plugin: 'antlr'
    
dependencies {
    compile "org.apache.commons:commons-math3:3.6.1"   
    compile "com.google.guava:guava:20.0"    
    compile "com.github.cirdles:LudwigLibrary:-SNAPSHOT"
    compile "com.github.cirdles:Commons:-SNAPSHOT"    
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.9'
    
    antlr "org.antlr:antlr4:4.6"
    compile 'org.antlr:antlr4-runtime:4.6'
   
    testCompile "com.github.cirdles:Commons:-SNAPSHOT"   
}

configurations.all {
    // Check for updates every build
       resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

sourceSets {
    main {
        antlr {
            srcDirs = ["src/main/antlr"]
        }
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

task fatJarCore(type: Jar) {
    baseName = 'CalamariCore'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def timestamp() {
    new SimpleDateFormat("dd MMMM yyyy").format(new Date())
}

processResources {
    filter ReplaceTokens, tokens: [
        "pom.version": version,
        "timestamp": timestamp()
    ]
}
